plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'com.github.romanqed'
archivesBaseName = 'jutils'
version = System.getenv('RELEASE_VERSION') ?: "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.github.romanqed', name: 'ranges', version: '2.0.0'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST
            description 'A set of simple utilities for implementing missing functionality in different versions of Java.'
            name 'jutils'
            url 'https://github.com/RomanQed/java-utils'
            organization {
                name 'com.github.romanqed'
                url 'https://github.com/RomanQed/'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/RomanQed/java-utils/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/RomanQed/java-utils/blob/main/LICENSE'
                }
            }
            scm {
                url 'https://github.com/RomanQed/java-utils'
                connection 'scm:https://github.com/RomanQed/java-utils.git'
                developerConnection 'scm:https://github.com/RomanQed/java-utils.git'
            }
            developers {
                developer {
                    id 'dev'
                    name 'RomanQed'
                    email 'gbakaldin@gmail.com'
                }
            }
        }
    }
}
